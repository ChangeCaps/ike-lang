type iter 'a = {
  next: {} -> option ('a, iter 'a)
}

fn iter::new : ({} -> option ('a, iter 'a)) -> iter 'a
fn iter::new next -> iter { next: next }

fn iter::next : iter 'a -> option ('a, iter 'a)
fn iter::next it {
  it.next {} 
}

fn iter::chain : iter 'a -> iter 'a -> iter 'a
fn iter::chain second first {
  iter::new |_| {
    match iter::next first {
      some (x, it) -> some <| x, iter::chain second it
      none         -> iter::next second
    }
  }
}

fn iter::map : ('a -> 'b) -> iter 'a -> iter 'b
fn iter::map f it {
  iter::new |_| {
    match iter::next it {
      some (x, it) -> some <| f x, iter::map f it
      none         -> none
    }
  }
}

fn iter::filter : ('a -> bool) -> iter 'a -> iter 'a
fn iter::filter f it {
  iter::new |_| {
    match iter::next it {
      some (x, it) -> {
        match f x {
          true  -> some <| x, iter::filter f it
          false -> iter::filter f it |> iter::next
        }
      }

      none -> none
    }
  }
}

fn iter::nth : int -> iter 'a -> option 'a
fn iter::nth n it {
  match iter::next it {
    none         -> none
    some (x, it) -> {
      match n {
        0 -> some x
        _ -> iter::nth (n - 1) it
      }
    }
  }
}

fn iter::range : int -> int -> iter int
fn iter::range start end {
  iter::new |_| {
    match start < end {
      true  -> some <| start, iter::range (start + 1) end
      false -> none
    }
  }
}

fn iter::each : ('a -> {}) -> iter 'a -> {}
fn iter::each f it {
  match iter::next it {
    some (x, it) -> {
      f x
      iter::each f it
    }
    none -> {}
  }
}
