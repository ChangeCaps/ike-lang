type option 'a = some 'a | none

fn option::assert : option 'a -> 'a
fn option::assert -> option::expect "option was none"

fn option::expect : str -> option 'a -> 'a
fn option::expect msg opt {
  match opt {
    some a -> a
    none   -> panic msg
  }
}

fn option::map : ('a -> 'b) -> option 'a -> option 'b
fn option::map f -> option::then |a| some (f a)

fn option::then : ('a -> option 'b) -> option 'a -> option 'b
fn option::then f opt {
  match opt {
    some a -> f a 
    none   -> none
  }
}

fn option::apply : 'a -> option ('a -> 'b) -> option 'b
fn option::apply a -> option::map |f| f a

fn option::some-or : 'a -> option 'a -> 'a
fn option::some-or a opt {
  match opt {
    some a -> a
    none   -> a
  }
}

fn option::ok-or : option 'a  -> 'e -> result 'a 'e  
fn option::ok-or opt e {
  match opt {
    some x -> ok x
    none   -> err e
  }
}

fn option::or-else : ({} -> option 'a) -> option 'a -> option 'a
fn option::or-else f opt {
  match opt {
    some a -> some a
    none   -> (f {})
  }
}

fn option::is-some : option 'a -> bool
fn option::is-some opt {
  match opt {
    some a -> true
    none   -> false
  }
}

fn option::is-some-and : ('a -> bool) -> option 'a -> bool
fn option::is-some-and f opt {
  match opt {
    some a -> f a
    none   -> false
  }
}
 
fn option::is-none-or : ('a -> bool) -> option 'a -> bool
fn option::is-none-or f opt {
  match opt {
    some a -> f a
    none   -> true
  }
}
